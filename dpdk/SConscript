# Contributed by Semihalf
#
# Copyright (c) 2014, 2015 Juniper Networks, Inc. All rights reserved.
#
import os

Import('VRouterEnv')
Import('dpdk_lib')

env = VRouterEnv.Clone()

if 'enableN3K' in env['ADD_OPTS']:
    env.SConscript('n3k/SConscript', exports = ['VRouterEnv', 'dpdk_lib'], duplicate = 0)

# Flags
env.Append(CCFLAGS = '-Werror -Wall')
env.Append(CCFLAGS = '-msse4.2')
if env['OPT'] == 'debug':
    env.Append(CCFLAGS = '-DCOMPILE_DEBUG_LOGS')

env.Append(CCFLAGS = '-DALLOW_EXPERIMENTAL_API')

# Include/lLib paths
env.Append(LIBPATH = [env['TOP_LIB']])
env.Append(LIBPATH = ['../host', '../sandesh', '../dp-core', 'n3k'])

# Libraries
env.Replace(LIBS = ['dp_core', 'dp_sandesh_c', 'dp_core', 'sandesh-c'])
env.Append(LIBS = ['rt', 'dl', 'pthread', 'urcu-qsbr', 'urcu-bp'])
if 'enableN3K' in env['ADD_OPTS']:
# TODO (FIX ME): Workaround to force n3k library build
    env.Append(LIBS = ['vr_dpdk_n3k'])
    env.Append(LINKFLAGS = ['-Wl,--whole-archive', '-lvr_dpdk_n3k', '-Wl,--no-whole-archive'])

env.Append(LINKFLAGS = env['DPDK_LINKFLAGS'])

dpdk_objs = env.Object(Glob('*.c'))
dpdk_vrouter = env.Program('contrail-vrouter-dpdk', dpdk_objs)
env.Requires(dpdk_objs, dpdk_lib)

# to make sure that all are built when you do 'scons' @ the top level
env.Default(dpdk_vrouter)

env.Alias('install', env.Install(env['INSTALL_BIN'], dpdk_vrouter))
env.Alias('install',
           env.InstallAs(env['INSTALL_SYSTEMD'] + '/contrail-vrouter-dpdk.service',
           '#/vrouter/systemd/contrail-vrouter-dpdk.service'))

# Comment the following lines till UT end if the build system doesnt have SSE3
# TODO: Add libpcap-devel, libpcap to buildRequires in spec before enabling this.
if 'vrouter-ut' in COMMAND_LINE_TARGETS:
    vtest = env.Command(target = "all_tests.log",
                source = "../utils/vtest/vtest",
                action =
                    "./vrouter/utils/vtest/all_tests_run"
                        + " -b " + Dir(env['TOP']).abspath
                        + " -x vrouter/utils/vtest/tests"
                        + " -l $TARGET"
                )
    env.Alias('vrouter-ut', vtest)
    env.Requires(vtest, dpdk_vrouter)

# UT end
def vrouter_coverage(target, source, env):
    os.system('lcov --base-directory . --directory' +  " ./build/coverage/vrouter/ " +
                ' -c --ignore-errors gcov -o vrouter_test.info')
    os.system('genhtml -o' + " ./build/coverage/vrouter/ " +
                  ' -t "code coverage" --num-spaces 4 vrouter_test.info')

if env['OPT'] == 'coverage':
    code_coverage = env.Command(Dir('.'), '', vrouter_coverage)
    env.AlwaysBuild(code_coverage)
    env.Alias('vrouter-ut:code_coverage', code_coverage)

# Local Variables:
# mode: python
# End:
